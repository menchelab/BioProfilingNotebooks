{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using CSV, StatsBase, Statistics, DataFrames, UMAP, RCall\n",
    "using Distributed, RMP, Random, Distributions\n",
    "using MultivariateStats, MultipleTesting, Dates\n",
    "using LinearAlgebra: I, Diagonal, diag, det, qr, Symmetric"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@rlibrary ggplot2\n",
    "@rlibrary extrafont"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "R\"\"\"\n",
    "# Used later for MCD computation\n",
    "\n",
    "library(robustbase)\n",
    "\n",
    "# Customize ggplot appearance\n",
    "\n",
    "library(ggplot2)\n",
    "library(extrafont)\n",
    "\n",
    "\n",
    "# Load extra fonts\n",
    "ttf_import(paths = \"/tmp/.fonts/\")\n",
    "loadfonts()\n",
    "\n",
    "# Change theme\n",
    "customTheme <- theme_light() + \n",
    "               theme(panel.grid.minor=element_blank(), text=element_text(size=17, family=\"Arial\", colour = \"#333333\"),\n",
    "                     line=element_line(colour = \"#333333\"), \n",
    "                     legend.background = element_rect(fill=alpha('#CCCCCC', 0.1)), legend.key = element_blank())\n",
    "\n",
    "# Change default colors\n",
    "scale_colour_continuous <- function (..., begin = 0.1, end = 0.9, direction = -1, option = \"plasma\", \n",
    "                                     type = getOption(\"ggplot2.continuous.colour\", default = \"viridis\")) {\n",
    "    switch(type, gradient = scale_colour_gradient(...), \n",
    "        viridis = scale_colour_viridis_c(option = option, begin = begin, end = end, direction = direction, ...), \n",
    "        stop(\"Unknown scale type\", call. = FALSE))\n",
    "}\n",
    "scale_color_continuous <- scale_colour_continuous\n",
    "\n",
    "scale_fill_continuous <- function (..., begin = 0.1, end = 0.9, direction = -1, option = \"plasma\", \n",
    "                                     type = getOption(\"ggplot2.continuous.colour\", default = \"viridis\")) {\n",
    "    switch(type, gradient = scale_fill_gradient(...), \n",
    "        viridis = scale_fill_viridis_c(option = option, begin = begin, end = end, direction = direction, ...), \n",
    "        stop(\"Unknown scale type\", call. = FALSE))\n",
    "\n",
    "}\n",
    "\n",
    "cemm_pal = colorRampPalette(c(\"#5A463C\", \"#008CAD\", \"#40B9D4\", \"#D4ECF2\", \"#D2323C\", \"#F8B100\", \"#DFDC00\"))\n",
    "scale_fill_discrete <- function (..., type = \"CeMM\", h = c(0, 360) + 15, c = 100, l = 65, h.start = 0, \n",
    "    direction = 1, na.value = \"grey50\", aesthetics = \"fill\") \n",
    "{\n",
    "    if (type == \"CeMM\"){\n",
    "        discrete_scale(aesthetics, \"CeMM\", cemm_pal, na.value = na.value, ...)\n",
    "    } else {\n",
    "        discrete_scale(aesthetics, \"hue\", hue_pal(h, c, l, h.start, \n",
    "            direction), na.value = na.value, ...)\n",
    "    }\n",
    "}\n",
    "\n",
    "scale_color_discrete <- function (..., type = \"CeMM\", h = c(0, 360) + 15, c = 100, l = 65, h.start = 0, \n",
    "    direction = 1, na.value = \"grey50\", aesthetics = \"colour\") {\n",
    "    if (type == \"CeMM\"){\n",
    "        discrete_scale(aesthetics, \"CeMM\", cemm_pal, na.value = na.value, ...)\n",
    "    } else {\n",
    "        discrete_scale(aesthetics, \"hue\", scales::hue_pal(h, c, l, h.start, \n",
    "            direction), na.value = na.value, ...)\n",
    "    }\n",
    "}\n",
    "scale_colour_discrete <- scale_color_discrete\n",
    "\n",
    "noGridTheme <- function(...){\n",
    "    theme(panel.grid.major=element_blank(), axis.text.x=element_text(size=12), axis.text.y=element_text(size=12),\n",
    "                      axis.line=element_line(color=\"#333333\", size = 0.2), panel.border = element_blank(), ...)\n",
    "}\n",
    "\n",
    "darkTheme <- function(...){\n",
    "    theme(panel.background = element_rect(fill = '#333333'), plot.background = element_rect(fill = '#333333'), \n",
    "          axis.line=element_line(color=\"#CCCCCC\", size = 0.2), \n",
    "          text=element_text(size=17, family=\"Arial\", colour = \"#CCCCCC\"),\n",
    "          line=element_line(colour = \"#CCCCCC\"))\n",
    "}\n",
    "\n",
    "theme_set(customTheme)\n",
    "\n",
    "options(repr.plot.width=10, repr.plot.height=10)\n",
    "\"\"\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Dataset generation\n",
    "We assume our data of interest to follow a multivariate normal distribution: In a morphological profiling, components are to some extent *independent (by removing correlated morphological features) and* normally distributed (by using a log-transformation)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"Generate reference and positive control datasets with the following parameters:\n",
    "* `NR` Number of points in control dataset  \n",
    "* `N` Number of points in other datasets  \n",
    "* `D` Number of dimensions in each dataset  \n",
    "* `pOutliers` Percentage of datasets contaminated with outliers  \n",
    "* `posScaling` Scaling of the transformation for positive controls  \n",
    "* `dimUMAP` Number of dimensions kept for UMAP distance computation\"\"\"\n",
    "generateDatasets = function(; NR = 4500, N = 1500, D = 100, pOutliers = 1/3, posScaling = 1.0, dimUMAP = 10)\n",
    "    Random.seed!(1);\n",
    "    \n",
    "    # The reference is centered on 0\n",
    "    µ = zeros(D);\n",
    "    \n",
    "    # Diagonal: variances follow a Gamma distribution of shape and scale parameters equal to 1 and 2\n",
    "    # Rationale: Some variability in scales with some high values, and no negative values\n",
    "    # NB: Beta distribution could be used instead of Gamma distribution if long-tail is not needed\n",
    "    # NB: Effects are smoothed by the orthogonal transformation anyway\n",
    "    distrib = Gamma(1,2)\n",
    "    sigma_diag = rand(distrib, D);\n",
    "    \n",
    "    # Now we transform this space by multiplying by a random orthogonal matrix\n",
    "    s = rand(D,D)\n",
    "    Q, R = qr(s);\n",
    "\n",
    "    # NB: becomes really slow, do not try with D > 500\n",
    "    ∑ = Q' * Diagonal(sigma_diag) * Q;\n",
    "\n",
    "    # Check that the matrix is symmetrical (up to machine error)\n",
    "    @assert all([∑[i,j] ≈ ∑[j,i] for i in 1:D for j in 1:D if j>i])\n",
    "\n",
    "    # Make it perfectly symmetrical\n",
    "    [∑[i,j] = ∑[j,i] for i in 1:D for j in 1:D if j>i]\n",
    "\n",
    "    # Sylvester's criterion of positive semidefinite matrices\n",
    "    @assert all([det(∑[1:size,1:size]) > 0 for size in 1:D])\n",
    "\n",
    "    # The reference still is centered (center µ)\n",
    "    # The outliers are not centered on 0 anymore\n",
    "    distrib = Normal(0, 1)\n",
    "    µOutliers = rand(distrib, D)\n",
    "\n",
    "    # The covariance of the outliers is similar but independent of the reference points\n",
    "    distrib = Gamma(1,2)\n",
    "    sigma_diagOutliers = rand(distrib, D);\n",
    "    \n",
    "    # Now we transform this space by multiplying by a random orthogonal matrix\n",
    "    s = rand(D,D)\n",
    "    Q, R = qr(s);\n",
    "\n",
    "    # NB: becomes really slow, do not try with D > 500\n",
    "    ∑Outliers = Q' * Diagonal(sigma_diagOutliers) * Q;\n",
    "\n",
    "    # Check that the matrix is symmetrical (up to machine error)\n",
    "    @assert all([∑Outliers[i,j] ≈ ∑Outliers[j,i] for i in 1:D for j in 1:D if j>i])\n",
    "\n",
    "    # Make it perfectly symmetrical\n",
    "    [∑Outliers[i,j] = ∑Outliers[j,i] for i in 1:D for j in 1:D if j>i]\n",
    "\n",
    "    # Sylvester's criterion of positive semidefinite matrices\n",
    "    @assert all([det(∑Outliers[1:size,1:size]) > 0 for size in 1:D])\n",
    "\n",
    "    # `100*pOutliers`% of the data will follow a multivariate normal distribution\n",
    "    # with the parameters we generated previously\n",
    "    distrib = MvNormal(µ, ∑);\n",
    "    distribOutliers = MvNormal(µOutliers, ∑Outliers);\n",
    "    matR = DataFrame(hcat(rand(distrib, Int(round(NR*(1-pOutliers)))),\n",
    "                      rand(distribOutliers, Int(round(NR*pOutliers))))')\n",
    "\n",
    "    Random.seed!(2);\n",
    "\n",
    "    # The reference is not centered on 0 anymore\n",
    "    distrib = Normal(0, 0.5*posScaling)\n",
    "    µmod = rand(distrib, D)\n",
    "\n",
    "    # The data will follow a multivariate normal distribution with the parameters\n",
    "    # we generated previously\n",
    "    distrib = MvNormal(µmod, ∑);\n",
    "    matP = DataFrame(hcat(rand(distrib, Int(round(N*(1-pOutliers)))),\n",
    "                      rand(distribOutliers, Int(round(N*pOutliers))))')\n",
    "    return(matR, matP)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Define distances of interest"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Compute the Mahalanobis Distance to center (MDC)\n",
    "    in a dataset 'data' for a given perturbation of indices 'iPert' \n",
    "    compared to a reference of indices 'iRef'.\"\"\"\n",
    "function MDC(data, iPert, iRef)\n",
    "    setPert = Matrix(data[iPert,:])\n",
    "    setRef = Matrix(data[iRef,:])\n",
    "\n",
    "    mdCenter = dropdims(mean(setRef, dims = 1), dims = 1)\n",
    "    mdCov = cov(setRef)\n",
    "\n",
    "    pertCenter = dropdims(mean(setPert, dims = 1), dims = 1)\n",
    "    \n",
    "    MD = mahalanobis(pertCenter, mdCenter, mdCov)\n",
    "    \n",
    "    return(MD)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Compute the median Mahalanobis Distance (MD)\n",
    "    in a dataset 'data' for a given perturbation of indices 'iPert' \n",
    "    compared to a reference of indices 'iRef'.\"\"\"\n",
    "function MD(data, iPert, iRef)\n",
    "    setPert = data[iPert,:]\n",
    "    setRef = Matrix(data[iRef,:])\n",
    "\n",
    "    mdCenter = dropdims(mean(setRef, dims = 1), dims = 1)\n",
    "    mdCov = cov(setRef)\n",
    "    \n",
    "    MD = median(map(x -> mahalanobis(x, mdCenter, mdCov), eachrow(setPert)))\n",
    "    return(MD)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Compute the median Robust Mahalanobis Distance (RMD)\n",
    "    in a dataset 'data' for a given perturbation of indices 'iPert' \n",
    "    compared to a reference of indices 'iRef'.\n",
    "    See https://e-archivo.uc3m.es/bitstream/handle/10016/24613/ws201710.pdf \"\"\"\n",
    "function RMD(data, iPert, iRef)\n",
    "    setPert = data[iPert,:]\n",
    "    setRef = data[iRef,:] \n",
    "\n",
    "    # Ensure that we have enough points to compute distance\n",
    "    if ((size(setPert)[1] < 2*size(data, 2))|(size(setRef)[1] < 2*size(data, 2)))\n",
    "        return(missing)\n",
    "    end\n",
    "    # NB: having less points than twice the number of dimensions leads to singularity\n",
    "    \n",
    "    # Compute Minimum Covariance Determinant and corresponding Robust Mahalanobis Distance\n",
    "    @rput setRef\n",
    "\n",
    "    R\"\"\"\n",
    "    set.seed(3895)\n",
    "    mcd <- covMcd(setRef)\n",
    "    mcdCenter <- mcd$center\n",
    "    mcdCov <- mcd$cov\n",
    "    \"\"\"\n",
    "    @rget mcdCenter\n",
    "    @rget mcdCov\n",
    "    \n",
    "    RMD = median(map(x -> mahalanobis(x, mcdCenter, mcdCov), eachrow(setPert)))\n",
    "    return(RMD)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Compute the Robust Hellinger Distance (RHD)\n",
    "    in a dataset `data` for a given perturbation of indices `iPert` \n",
    "    compared to a reference of indices `iRef`.\"\"\"\n",
    "function RHD(data, iPert, iRef)\n",
    "    setPert = data[iPert,:]\n",
    "    setRef = data[iRef,:] \n",
    "\n",
    "    # Ensure that we have enough points to compute distance\n",
    "    if ((size(setPert)[1] < 2*size(data, 2))|(size(setRef)[1] < 2*size(data, 2)))\n",
    "        return(missing)\n",
    "    end\n",
    "    # NB: having less points than twice the number of dimensions leads to singularity\n",
    "    \n",
    "    # Compute Minimum Covariance Determinant and corresponding Robust Hellinger Distance\n",
    "    @rput setRef\n",
    "    @rput setPert\n",
    "\n",
    "    R\"\"\"\n",
    "    set.seed(3895)\n",
    "    mcd1 <- covMcd(setRef)\n",
    "    mcdCenter1 <- mcd1$center\n",
    "    mcdCov1 <- mcd1$cov\n",
    "    \n",
    "    # We set the seed twice to always\n",
    "    # find the same estimators given\n",
    "    # the same sample\n",
    "    set.seed(3895)\n",
    "    mcd2 <- covMcd(setPert)\n",
    "    mcdCenter2 <- mcd2$center\n",
    "    mcdCov2 <- mcd2$cov\n",
    "    \"\"\"\n",
    "    @rget mcdCenter1\n",
    "    @rget mcdCov1\n",
    "    @rget mcdCenter2\n",
    "    @rget mcdCov2\n",
    "    \n",
    "    RHD = hellinger(mcdCenter1, mcdCov1, mcdCenter2, mcdCov2)\n",
    "    return(RHD)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Compare raw distances"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"Plot evolution of multiple metrics based on a previous exploration of the parameter space\"\"\"\n",
    "function savePlotSeries(resMetrics, paramSpace; refMetrics = nothing,\n",
    "                        plotTitle = \"Distance dynamics\", plot = false, save = true)\n",
    "    outFolder = Dates.format(Dates.now(), \"yyyy-mm-dd_HH-MM-SS\")\n",
    "    mkpath(\"Fig\")\n",
    "    mkpath(\"Fig/0c\")\n",
    "    mkpath(\"Fig/0c/\"*outFolder)\n",
    "    for (s,m) in resMetrics\n",
    "        if !isnothing(refMetrics)\n",
    "            ggDF = DataFrame(x = repeat(paramSpace, 2), y = vcat(m, refMetrics[s]), \n",
    "                             c = repeat([\"Pert\",\"Ref\"], inner = length(paramSpace)))\n",
    "            ggp = ggplot(ggDF) + aes(x = :x, y = :y, color = :c) + labs(color = \"Distance to\")\n",
    "        else\n",
    "            ggDF = DataFrame(x = paramSpace, y = m)\n",
    "            ggp = ggplot(ggDF) + aes(x = :x, y = :y) \n",
    "        end\n",
    "        ggp = ggp + geom_point() + xlab(\"Perturbation scaling\") + \n",
    "              ylab(s) + ggtitle(plotTitle)\n",
    "        if plot\n",
    "            print(ggp)\n",
    "        end\n",
    "        if save\n",
    "            ggsave(\"Fig/0c/\"*outFolder*'/'s*\".pdf\", ggp)\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "# Parameter space to parse\n",
    "paramSpace = range(0,1,step = 0.02)\n",
    "NR = 300\n",
    "N = 150\n",
    "D = 2\n",
    "pOutliers = 0\n",
    "\n",
    "print(now())\n",
    "allMDCraw = Array{Float64,1}()\n",
    "allMDraw = Array{Float64,1}()\n",
    "allRMDraw = Array{Float64,1}()\n",
    "allRHDraw = Array{Float64,1}()\n",
    "refMDCraw = Array{Float64,1}()\n",
    "refMDraw = Array{Float64,1}()\n",
    "refRMDraw = Array{Float64,1}()\n",
    "refRHDraw = Array{Float64,1}()\n",
    "\n",
    "for param in paramSpace\n",
    "    df = vcat(generateDatasets(posScaling = param, pOutliers = pOutliers, NR = NR, N = N, D = D)...)\n",
    "    # Remember how these records were generated\n",
    "    origDataset = vcat(repeat([\"Reference\"], NR),\n",
    "                       repeat([\"Positive control\"], N));\n",
    "    \n",
    "    MDCraw = map(x -> MDC(df, origDataset.==x,\n",
    "                origDataset.==\"Reference\"), unique(origDataset))\n",
    "    MDraw = map(x -> MD(df, origDataset.==x,\n",
    "                       origDataset.==\"Reference\"), unique(origDataset))\n",
    "    RMDraw = map(x -> RMD(df, origDataset.==x, \n",
    "                origDataset.==\"Reference\"), unique(origDataset))\n",
    "    RHDraw = map(x -> RHD(df, origDataset.==x, origDataset.==\"Reference\"),\n",
    "                unique(origDataset))\n",
    "    \n",
    "    # Store results\n",
    "    append!(allMDCraw, MDCraw[2])\n",
    "    append!(allMDraw, MDraw[2])\n",
    "    append!(allRMDraw, RMDraw[2])\n",
    "    append!(allRHDraw, RHDraw[2])\n",
    "    \n",
    "    append!(refMDCraw, MDCraw[1])\n",
    "    append!(refMDraw, MDraw[1])\n",
    "    append!(refRMDraw, RMDraw[1])\n",
    "    append!(refRHDraw, RHDraw[1])    \n",
    "    \n",
    "    # Display results\n",
    "    println(string(now())*\" - \"*string(param))\n",
    "    println(MDCraw, MDraw, RMDraw, RHDraw)\n",
    "end\n",
    "\n",
    "resMetrics = Dict(\"Mahalanobis distance from center\" => allMDCraw,\n",
    "                  \"Median Mahalanobis distance\" => allMDraw,\n",
    "                  \"Robust Mahalanobis distance\" => allRMDraw,\n",
    "                  \"Robust Hellinger distance\" => allRHDraw)\n",
    "refMetrics = Dict(\"Mahalanobis distance from center\" => refMDCraw,\n",
    "                  \"Median Mahalanobis distance\" => refMDraw,\n",
    "                  \"Robust Mahalanobis distance\" => refRMDraw,\n",
    "                  \"Robust Hellinger distance\" => refRHDraw)\n",
    "plotTitle = \"NR/N/D/pO: \"*string(NR)*'/'*string(N)*'/'*string(D)*'/'*string(pOutliers)\n",
    "\n",
    "savePlotSeries(resMetrics, paramSpace, refMetrics = refMetrics,\n",
    "               plotTitle = plotTitle, save = false, plot = true)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.4.1",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
